buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath "de.dfki.mary:gradle-marytts-crossvalidation-plugin:0.1-SNAPSHOT"
    }
}

plugins {
    id 'de.dfki.mary.voicebuilding-festvox' version '5.2.2'
    id 'de.dfki.mary.voicebuilding-legacy' version '5.2.2'
    id 'de.undercouch.download' version '3.2.0'
}

group 'de.dfki.mary'
version '5.3-SNAPSHOT'

apply plugin: 'de.dfki.mary.voicebuilding.marytts-crossvalidation'

voice {
    name = 'cmu-rms'
    language = 'en'
    region = 'US'
    gender = 'male'
    type = 'unit selection'
    description = 'A male US English unit selection voice, built from recordings provided by Carnegie Mellon University'
    license {
        name = 'Arctic License'
        shortName = 'Arctic'
        url = 'http://mary.dfki.de/download/arctic-license.html'
    }
    samplingRate = 16000
}

repositories {
    ivy {
        url 'http://mary.dfki.de/repo'
        layout 'maven'
    }
    ivy {
        url 'http://festvox.org/cmu_arctic/cmu_arctic/packed/'
        layout 'pattern', {
            artifact '[module]-[revision]-[classifier].[ext]'
        }
    }
}

dependencies {
    data group: 'org.festvox', name: 'cmu_us_rms_arctic', version: '0.95', classifier: 'release', ext: 'tar.bz2'
}

import de.undercouch.gradle.tasks.download.Download

task downloadMausLabFiles(type: Download) {
    src 'https://github.com/insa-k/voice-cmu-rms-maus-data/releases/download/MAUS-lab/lab.zip'
    dest "$buildDir/lab.zip"
    overwrite false
    onlyIfNewer true
}

task unpackLab(type: Copy) {
    from zipTree("$buildDir/lab.zip"), {
        include '**/lab/*.lab'
        eachFile {
            it.path = it.name
        }
    }
    into "$buildDir/lab"
    includeEmptyDirs = false
    dependsOn downloadMausLabFiles
}

selectCrossvalidationFiles {
    dependsOn wav, text, unpackLab
    selectCrossvalidationFiles.excludeList = ["arctic_a0438", "arctic_a0439", "arctic_b0391"]
}

generateCrossvalidationInputFiles.dependsOn selectCrossvalidationFiles

legacyInit.dependsOn wav, text, lab, selectCrossvalidationFiles, generateCrossvalidationInputFiles

synthesizeCrossvalidationAudio.dependsOn('build')

getRealisedDurations.dependsOn synthesizeCrossvalidationAudio

runCrossvalidation.dependsOn getRealisedDurations, synthesizeCrossvalidationAudio, moveReferenceFiles
